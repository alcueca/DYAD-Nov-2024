# Kerosine vaults are not liquidated

## Impact
In a liquidation, kerosine vaults are not liquidated. Malicious users would create vaults that are only overcollateralized by kerosine. When the price of exogenous collaterals drops, liquidators would be liquidating the positions at a loss. Given that, the undercollateralized positions would become bad debt for the protocol. If the vault holders would be liquidated, they would get their kerosine back.

## Severity
Likelihood: High - It is currently broken.
Impact: High - Protocol loses money.
Severity: High

## Description
In VaultManagerV2.liquidate#L223 we loop through the vaults of the DNft, and liquidate them. However, the assets in `vaultsKerosene` are not included in this loop, and the liquidated dNft would keep them.

The amount of collateral to liquidate is equal to the debt plus a 20% of the overcollateralization. If a position has a collateralization ratio of 150%, with one third of that being kerosine, the liquidator would get back 76% of the value thy used to repay the underwater position. 

## Proof of Concept
Please be aware that a number of patches had to be applied to the codebase to make this test work. These are clearly marked in the code below.

Insert the following test into `test/fork/v2.t.sol`, and run it on a mainnet fork:

```
  function testLiquidateButKerosine_Debug() public {
    // We license the vault manager to be able to mint DYAD
    vm.prank(MAINNET_OWNER);
    Licenser(contracts.vaultLicenser).add(address(contracts.vaultManager));

    DNft dNft = DNft(MAINNET_DNFT);

    // Add the vault manager to the OLD licenser to allow minting DYAD
    vm.prank(MAINNET_OWNER);
    Licenser(0xd8bA5e720Ddc7ccD24528b9BA3784708528d0B85).add(address(contracts.vaultManager));

    // We reset the DNft contract to be able to mint free DNfts
    vm.etch(address(MAINNET_DNFT), address(new DNft()).code);
    dNft = DNft(MAINNET_DNFT);

    uint id = dNft.mintNft(address(this));

    // We patch the Unbounded Kerosine Vault so that it doesn't count the v1 DYAD
    vm.etch(
      address(contracts.unboundedKerosineVault),
      address(
        new UnboundedKerosineVaultFixed(
          contracts.vaultManager,
          Kerosine(MAINNET_KEROSENE), 
          Dyad    (MAINNET_DYAD),
          contracts.kerosineManager
        )
      ).code
    );

    // We patch the VaultManager so that it uses the Licenser to decide on adding kerosene vaults to DNfts
    vm.etch(
      address(contracts.vaultManager),
      address(
        new VaultManagerV2Fixed(
        DNft(MAINNET_DNFT),
        Dyad(MAINNET_DYAD),
        contracts.vaultLicenser
      )
      ).code
    );

    // Obtain weth
    vm.prank(MAINNET_WETH); // deal not working for me
    ERC20(MAINNET_WETH).transfer(address(this), 1 ether);

    // Add the weth vault and deposit 1 eth    
    contracts.vaultManager.add(id, address(contracts.ethVault));
    ERC20(MAINNET_WETH).approve(address(contracts.vaultManager), 1 ether);
    contracts.vaultManager.deposit(id, address(contracts.ethVault), 1 ether);

    // Obtain kerosene
    vm.prank(MAINNET_OWNER);
    ERC20(MAINNET_KEROSENE).transfer(address(this), 1 ether); // This is a huge amount because the surplus collateral is low and the unitary price of kerosine is also low.

    // Add the kerosene vault and deposit kerosene
    contracts.vaultManager.addKerosene(id, address(contracts.unboundedKerosineVault));
    ERC20(MAINNET_KEROSENE).approve(address(contracts.vaultManager), 1 ether);
    contracts.vaultManager.deposit(id, address(contracts.unboundedKerosineVault), 1 ether);

    // Mint dyad
    contracts.vaultManager.mintDyad(id, 1 ether, address(this));

      // Get a destination dNFT
    uint to = dNft.mintNft(address(this));

    // Unlicense the eth and wsteth vaults to drop the value of the victim's collateral to zero
    vm.startPrank(MAINNET_OWNER);
    contracts.vaultLicenser.remove(address(contracts.ethVault));
    contracts.vaultLicenser.remove(address(contracts.wstEth));
    vm.stopPrank();

    // Liquidate
    contracts.vaultManager.liquidate(id, to);

    // We still have the kerosine in the vault
    assertGt(contracts.unboundedKerosineVault.id2asset(id), 0);
  }
```

## Recommendation
Liquidate the kerosine vaults as well.
